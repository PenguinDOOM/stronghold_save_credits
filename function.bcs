// from bcsutils

raw Function_Ints = -1;

enum : int { PORT_ZDOOM = 1 };
enum : int { PORT_GZDOOM = 2 };
enum : int { PORT_ZANDRONUM = 3 };

function bool SetActivatorToPlayer2(int player)
{
	if (SetActivatorToPlayer(player))
		return true;
		
	if (player >= 0 && player < 8)
		return false;
		
	return SetActivator(0, AAPTR_PLAYER1<<player);
}

function int IdentifySourcePort(void)
{
	if (Function_Ints != -1)
		return Function_Ints;

	if (GetPlayerAccountName(0) != (str)0)
	{
		// GetPlayerAccountName returns a string (possibly empty) in Zandronum
		// but 0 in GZDoom.
		Function_Ints = PORT_ZANDRONUM;
		return Function_Ints;
	}
	
	int tid = UniqueTid();
	if (SpawnForced("DynamicLight", (fixed)0, (fixed)0, (fixed)0, tid))
	{
		// DynamicLight is a built-in actor in GZDoom.
		Thing_Remove(tid);
		Function_Ints = PORT_GZDOOM;
		return Function_Ints;
	}
	
	Function_Ints = PORT_ZDOOM;
	return Function_Ints;
}

function str GetPortName(void)
{
	int port = IdentifySourcePort();
	switch (port)
	{
	case PORT_ZDOOM:
		return "ZDoom";
	case PORT_GZDOOM:
		return "GZDoom";
	case PORT_ZANDRONUM:
		return "Zandronum";
	}

	return StrParam(s:"IdentifySourcePort() returned ", d:port);
}

function bool IsZandronum(void)
{
	return IdentifySourcePort() == PORT_ZANDRONUM;
}

function bool IsGZDoom(void)
{
	int port = IdentifySourcePort();
	return port == PORT_GZDOOM || port == PORT_ZANDRONUM;
}

function bool IsServer(void)
{
	if (ConsolePlayerNumber() < 0)
		return true;
		
	// Needs to be done like this to avoid crashing the script in ZDoom.
	if (IsZandronum())
		return !IsNetworkGame();
	
	return true;
}

function bool IsClient(void)
{
	return ConsolePlayerNumber() >= 0;
}

function bool IsServerOnly(void)
{
	return IsServer() && !IsClient();
}

function bool IsClientOnly(void)
{
	return IsClient() && !IsServer();
}

function bool StrEquals(str a, str b)
{
    return StrCmp(a, b) == 0;
}	

function bool StrIEquals(str a, str b)
{
	return StrICmp(a, b) == 0;
}